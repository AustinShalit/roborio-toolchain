# Maintainer: Tyler Veness <calcmogul at gmail dot com>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

_target_arch=arm
_target=${_target_arch}-frc-linux-gnueabi
pkgname=${_target}-linux-api-headers
pkgver=3.2.35
_basever=3.2.35
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace (${_target})"
arch=(any)
url="http://www.gnu.org/software/libc"
license=('GPL2')
makedepends=("git")
provides=("${_target}-linux-api-headers=${_basever}"
  "${_target}-linux-api-headers30")
conflicts=("${_target}-linux-api-headers26")
source=("git+git://git.kernel.org/pub/scm/linux/kernel/git/rt/linux-stable-rt.git#tag=v3.2.35-rt52")
md5sums=('SKIP')

build() {
  cd "${srcdir}/linux-stable-rt"

  make ARCH=${_target_arch} mrproper
  make ARCH=${_target_arch} headers_check
}

package() {
  cd "${srcdir}/linux-stable-rt"

  make INSTALL_HDR_PATH="${pkgdir}/usr/${_target}/" ARCH=${_target_arch} V=0 \
    CROSS_COMPILE=${_target}- \
    headers_install

  # clean-up unnecessary files generated during install
  find "${pkgdir}" -name .install -or -name ..install.cmd | xargs rm -f
}
